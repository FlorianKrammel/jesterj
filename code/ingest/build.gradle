/*
 * Copyright 2013-2017 Needham Software LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
  repositories {
    jcenter()
    flatDir {
      dir 'onejar'
    }

  }
  dependencies {
    //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
    classpath 'com.github.rholder:gradle-one-jar:1.0.4'
  }
}

plugins {
  id 'com.github.jk1.dependency-license-report' version '0.3.4'
}

allprojects {
  apply plugin: "com.jfrog.artifactory"
}


artifactory {
  contextUrl = "https://jesterj.jfrog.io/jesterj"   //The base Artifactory URL if not overridden by the publisher/resolver
  publish {
    repository {
      repoKey = 'libs-snapshot-local'
      /*

        System env var is for Travis CI, add these to gradle.properties for local builds

        jj_artifactory_user=anonymous
        jj_artifactory_password=

        Note that anonymous user will not be able to test out new versions of libraries. Use mavenLocal() for that.

       */
      username = project.hasProperty("jj_artifactory_user") ? project.getProperty("jj_artifactory_user") : System.env.ARTIFACTORY_USER
      password = project.hasProperty("jj_artifactory_password") ? project.getProperty("jj_artifactory_password") : System.env.ARTIFACTORY_PASSWORD
      maven = true

    }
    defaults {
      publications('mavenJava')
      publishArtifacts = true
      publishPom = true
    }
  }
  resolve {
    repository {
      repoKey = 'libs-release'
      username = project.hasProperty("jj_artifactory_user") ? project.getProperty("jj_artifactory_user") : System.env.ARTIFACTORY_USER
      password = project.hasProperty("jj_artifactory_password") ? project.getProperty("jj_artifactory_password") : System.env.ARTIFACTORY_PASSWORD
      maven = true

    }
  }
}


apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'gradle-one-jar'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

licenseReport {
  excludeGroups = ['jini']
}

repositories {
  maven {
    url 'https://clojars.org/repo'
  }
  maven {
    credentials {
      username = project.hasProperty("jj_artifactory_user") ? project.getProperty("jj_artifactory_user") : System.env.ARTIFACTORY_USER
      password = project.hasProperty("jj_artifactory_password") ? project.getProperty("jj_artifactory_password") : System.env.ARTIFACTORY_PASSWORD
    }
    url 'https://jesterj.jfrog.io/jesterj/libs-release/'
  }
  flatDir {
    dir 'onejar'
  }
}

configurations {
  oneJarLib
}
version = '0.2-SNAPSHOT'
group = 'org.jesterj'

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId project.group
      artifactId 'ingest'
      version project.version
      from components.java
      artifact oneJar {
        classifier "node"
      }
    }
  }
}

mainClassName = 'foo.bar.FOO'

jacocoTestReport {
  reports {
    xml.enabled true
  }
}
dependencies {
  compile 'org.apache.tika:tika-parsers:1.12'
  compile 'org.apache.solr:solr-solrj:5.5.0'
  compile 'org.elasticsearch:elasticsearch:2.3.1'

  compile 'com.google.guava:guava:18.0'
  compile 'com.google.code.findbugs:jsr305:3.0.0'

  compile 'jini:jini-core:2.1'
  compile 'org.apache.river:reggie:2.2.2'
  compile 'org.apache.cassandra:cassandra-all:3.11.0-JJ'
  compile 'com.datastax.cassandra:cassandra-driver-core:3.2.0'
  compile 'org.yaml:snakeyaml:1.14'
  compile 'org.apache.velocity:velocity:1.7'

  compile 'org.apache.logging.log4j:log4j-core:2.5'
  compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.5'
  compile 'org.apache.logging.log4j:log4j-jcl:2.5'
  compile 'org.apache.logging.log4j:log4j-1.2-api:2.5'
  compile 'org.apache.logging.log4j:log4j-jul:2.5'

  compile('com.needhamsoftware:easier-mock:2.0') {
    ext.transative = false
  }
  compile 'net.jini:jsk-platform:2.2.2'
  compile 'docopt:docopt:0.6.1'
  testCompile 'junit:junit:4.11'
  testCompile 'com.needhamsoftware:easier-mock:2.0'
  //runtime ':one-jar-boot:0.98' // uncomment for debugging stack traces into one-jar classes

  oneJarLib files('onejar/one-jar-boot-0.98.jar')
  compile files('onejar/one-jar-boot-0.98.jar')
  compile('org.reflections:reflections:0.9.10') {
    exclude group: 'org.javassist'
  }
  compile 'org.javassist:javassist:3.20.0-GA' // substitute a version that handles java 8

}

task oneJar(type: OneJar) {
  mainClass = 'org.jesterj.ingest.Main'
  archiveName = 'ingest-'+project.version+'-node.jar'
  useStable = false
  //noinspection GroovyAssignabilityCheck
  manifest {
    //noinspection GroovyAssignabilityCheck
    attributes 'One-Jar-URL-Factory': 'com.simontuffs.onejar.JarClassLoader$OneJarURLFactory'
  }
  oneJarConfiguration = configurations.oneJarLib
}

jacocoTestReport.dependsOn test

artifactoryPublish.dependsOn oneJar
